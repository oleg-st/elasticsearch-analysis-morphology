import org.gradle.util.internal.VersionNumber

/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:$version"
    }
}

apply plugin: 'java'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.yaml-rest-test'

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

esplugin {
    description 'English and Russian Morphology for ElasticSearch.'
    classname 'org.elasticsearch.plugin.analysis.morphology.AnalysisMorphologyPlugin'
    name 'analysis-morphology'
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}
esplugin.version = version

configurations {
    yamlRestTestRuntimeOnly.extendsFrom testRuntimeOnly
    restTestSpecs
}

test {
    /* A workaround is to disable the security manager during test
       Java 17+:
       "unable to install test security manager"
       "The Security Manager is deprecated and will be removed in a future release" */
    jvmArgs '-Dtests.security.manager=false'
}

yamlRestTest {
    /* A workaround is to disable the security manager during test
       Java 17+:
       "unable to install test security manager"
       "The Security Manager is deprecated and will be removed in a future release" */
    jvmArgs '-Dtests.security.manager=false'
}

// use morph 1.8 for 8.15.0+, 1.7 for 8.7.0+, 1.6 for 8.0+, 1.5 for 7.0+
VersionNumber versionNumber = VersionNumber.parse(version as String);
if (versionNumber >= VersionNumber.parse('8.15.0')) {
    ext.morphVersion = '1.8'
} else if (versionNumber >= VersionNumber.parse('8.7.0')) {
    ext.morphVersion = '1.7'
} else if (versionNumber >= VersionNumber.parse('8.0.0')) {
    ext.morphVersion = '1.6'
} else if (versionNumber >= VersionNumber.parse('7.0.0')) {
    ext.morphVersion = '1.5'
} else {
    throw new GradleException("Unsupported elasticsearch version ${version}")
}

dependencies {
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.17.2'
    yamlRestTestRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.17.2'
    implementation "org.apache.lucene.morphology:russian:${project.morphVersion}"
    implementation "org.apache.lucene.morphology:english:${project.morphVersion}"
    implementation "org.apache.lucene.morphology:morph:${project.morphVersion}"
}
