/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:$version"
    }
}

apply plugin: 'java'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.yaml-rest-test'

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

esplugin {
    description 'English and Russian Morphology for ElasticSearch.'
    classname 'org.elasticsearch.plugin.analysis.morphology.AnalysisMorphologyPlugin'
    name 'analysis-morphology'
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}
esplugin.version = version

configurations {
    yamlRestTestRuntimeOnly.extendsFrom testRuntimeOnly
    restTestSpecs
}

test {
    /* Workaround for Java 17+ build:
        "unable to install test security manager"
        "The Security Manager is deprecated and will be removed in a future release" */
    jvmArgs '-Djava.security.manager=allow'
}

yamlRestTest {
    /* Workaround for Java 17+ build:
        "unable to install test security manager"
        "The Security Manager is deprecated and will be removed in a future release" */
    jvmArgs '-Djava.security.manager=allow'
}

// use morph 1.6 for 8.x, 1.5 for 7.x
if (version ==~ /8\.[\d.]*/) {
    ext.morphVersion = '1.6'
} else if (version ==~ /7\.[\d.]*/) {
    ext.morphVersion = '1.5'
} else {
    throw new GradleException("Unsupported elasticsearch version {$version}")
}

dependencies {
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.17.2'
    yamlRestTestRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.17.2'
    implementation "org.apache.lucene.morphology:russian:${project.morphVersion}"
    implementation "org.apache.lucene.morphology:english:${project.morphVersion}"
    implementation "org.apache.lucene.morphology:morph:${project.morphVersion}"
}
